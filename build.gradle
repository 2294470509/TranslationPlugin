import org.apache.tools.ant.filters.EscapeUnicode

buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
}

plugins {
    // https://github.com/JetBrains/gradle-intellij-plugin
    id "org.jetbrains.intellij" version "0.4.14"
    id 'org.jetbrains.kotlin.jvm' version '1.3.61'
}

repositories {
    jcenter()
    mavenCentral()
}

group = 'cn.yiiguxing.plugin.translate'
version = "${version}${buildNumber ? '-' + buildNumber : ''}"

intellij {
    version = ideaVersion
    pluginName = 'TranslationPlugin'
    downloadSources = Boolean.valueOf(sources)
    sandboxDirectory 'sandbox'
    plugins "org.jetbrains.kotlin:1.2.40-release-IJ2017.1-1", "Pythonid:2017.1.171.3780.116", "Dart:171.4006"
}

patchPluginXml {
    sinceBuild customSinceBuild
    untilBuild customUntilBuild
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'

    compile 'org.jsoup:jsoup:1.12.1'
    compile 'org.apache.commons:commons-dbcp2:2.6.0'
    compile 'commons-dbutils:commons-dbutils:1.7'
    compile('com.googlecode.soundlibs:mp3spi:1.9.5.4') {
        exclude module: 'junit'
    }
}

sourceCompatibility = javaVersion
targetCompatibility = javaTargetVersion

compileKotlin {
    kotlinOptions {
        languageVersion = kotlinLanguageVersion
        apiVersion = kotlinTargetVersion
        jvmTarget = javaTargetVersion
    }
}
compileTestKotlin {
    kotlinOptions {
        languageVersion = kotlinLanguageVersion
        apiVersion = kotlinTargetVersion
        jvmTarget = javaTargetVersion
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(ProcessResources) {
    filesMatching('**/*.properties') {
        filter EscapeUnicode
    }
}
